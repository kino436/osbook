OUTPUT_FORMAT("elf32-h8300")
OUTPUT_ARCH(h8300h)
ENTRY("_start")

MEMORY
{
/* RAMの定義 */
	ramall(rwx)	: org = 0xffbf20, len = 0x004000 /* 16KB */
	ram(rwx)	: org = 0xffc020, len = 0x003f00
	stack(rw)	: org = 0xffff00, len = 0x000000 /* end of RAM */
}

SECTIONS
{
	.text : {	/* テキスト領域 */
		_test_start = . ;	/* textセクションの先頭 */
		*(.text)
		_etext = . ;		/* textセクションの終端 */
	} > ram

	.rodata : {
		_rodata_start = . ;
		*(.strings)
		*(.rodata)
		*(.rodata.*)
		_erodata = . ;
	} > ram

	.data : {	/* データ領域 */
		_data_start = . ;
		*(.data)
		_edata = . ;
	} > ram

	.bss : {	/* BSS領域 */
		_bss_start = . ;
		*(.bss)
		*(COMMON)
		_ebss = . ;
	} > ram

	. = ALIGN(4);	/* 4バイトアライメント */
	_end = . ;	/* セクションの終端 */

	.stack : {
		_stack = . ;	/* スタックを指すシンボル */
	} > stack
}
